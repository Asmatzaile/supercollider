CLASS:: DrawGrid
summary:: Draws grid lines on a UserView for plotting
categories:: GUI>Accessories
related:: Reference/plot, Classes/AbstractGridLines, Classes/GridLines, Classes/Plotter, Classes/UserView

DESCRIPTION::
code::DrawGrid:: is used to draw link::Classes/GridLines:: and its labels on a link::Classes/UserView::. It is notably used by link::Classes/Plotter:: to draw the grid lines on a plot but can also be used to add grid lines to any code::UserView::, e.g. behind sliders or another GUI element.

See the example below for its link::#Basic use in a UserView#basic use in a UserView::.

Note that code::DrawGrid:: does not hold any reference to the code::UserView:: but is meant to have its code::-draw:: method called inside of the code::-drawFunc:: of the code::UserView::.  It only needs to know what bounds in which to draw the grid lines and what the horizontal and vertical code::GridLines:: are.


CLASSMETHODS::

METHOD:: new

argument:: bounds
A link::Classes/Point:: or link::Classes/Rect:: describing the size and position of the grid within the parent view (not including any labels).

argument:: horzGrid
A grid lines object for the x-axis, instantiated via link::Classes/GridLines::, or link::Classes/ControlSpec#-grid:: method, or code::nil:: (resulting in no grid lines).

argument:: vertGrid
A grid lines object for the y-axis, see strong::horzGrid::.

returns:: A code::DrawGrid::.

discussion::
For the strong::horizGrid:: and strong::vertGrid:: arguments, link::Classes/GridLines:: or link::Classes/ControlSpec#-grid:: will instantiate a subclass of link::Classes/AbstractGridLines::, such as link::Classes/LinearGridLines:: or link::Classes/ExponentialGridLines::, based on the code::ControlSpec::'s warp behavior.

Multiple code::DrawGrid:: may be used to draw grids on a single link::Classes/UserView::.


METHOD:: test
For testing a new link::Classes/AbstractGridLines:: object.
code::
DrawGrid.test(\freq.asSpec.grid, \amp.asSpec.grid);
::
code::
DrawGrid.test(nil, \lofreq.asSpec.grid);
::

argument:: horzGrid
A grid lines object for the x-axis, instantiated via link::Classes/GridLines::, or link::Classes/Spec#-grid:: method, or code::nil:: (resulting in a link::Classes/BlankGridLines::).

argument:: vertGrid
A grid lines object for the y-axis, see strong::horzGrid::.

argument:: bounds
A link::Classes/Point:: or link::Classes/Rect:: describing the size and position of the grid within the parent view (not including any labels).
Default: code::500 @ 400::.

returns:: A code::DrawGridTest:: (a subclass of code::DrawGrid::).

discussion::
See link::#-test:: if you'd like to preview or test modifications of a code::DrawGrid:: that has already been created. Also see link::#Testing and modifying#Examples:: below.

Inspecting the source code of code::DrawGridTest:makeWindow:: can serve as a guide on how to embed a code::DrawGrid:: in a code::UserView::.


INSTANCEMETHODS::

METHOD:: draw
This draws to the currently active link::Classes/UserView::. This method is meant to be called from inside the link::Classes/UserView#-drawFunc#-drawFunc:: of a code::UserView::.

returns:: code::nil::

discussion::
See the example below for its link::#Basic use in a UserView#basic use in a UserView::, including how to manage its bounds when the enclosing view resizes.


METHOD:: horzGrid
Set the x-axis grid lines.

argument:: g
An link::Classes/AbstractGridLines:: subclass, instantiated via link::Classes/GridLines::, or link::Classes/ControlSpec#-grid:: method, or code::nil:: (resulting in no grid lines).

returns:: Self.


METHOD:: vertGrid
Set the y-axis grid lines.

argument:: g
An link::Classes/AbstractGridLines:: subclass, instantiated via link::Classes/GridLines::, or link::Classes/ControlSpec#-grid:: method, or code::nil:: (resulting in no grid lines).

returns:: Self.


METHOD:: bounds
Get/set bounds describing the extents of the grid (not including any labels).

argument:: b
A link::Classes/Rect::.

returns:: A link::Classes/Rect::.


METHOD:: font
Get/set the font used by the grid lines labels.

argument:: f
A link::Classes/Font::.

returns:: A link::Classes/Font::.

METHOD:: fontColor
Get/set the font color.

argument:: c
A link::Classes/Color::.

returns:: A link::Classes/Color::.

METHOD:: gridColors
Set the colors of the grid lines for each axis.

argument:: colors
An link::Classes/Array:: of two colors for the x and y grid lines, respectively.

returns:: Self.

METHOD:: opacity
Get/set opacity.

returns:: A code::Float::.

METHOD:: smoothing
A code::Boolean:: which turns on/off anti-aliasing. See link::Classes/Pen#*smoothing::.

returns:: A code::Boolean::.

METHOD:: linePattern
Set the line dash pattern. The strong::value:: should be a link::Classes/FloatArray:: of values that specify the lengths of the alternating dashes and spaces.
For example, code::FloatArray[10.0, 3.0, 5.0, 3.0]::, for dashes of lengths code::10.0:: and code::5.0:: pixels, separated by spaces of code::3.0:: pixels.
See link::Classes/Pen#*lineDash::.

returns:: Self.

METHOD:: x
A code::DrawGridX:: object that draws the x (horizontal) axis. In
general you shouldn't need to set this.

returns:: A code::DrawGridX::.

METHOD:: y
A code::DrawGridY:: object that draws the y (vertical) axis. In general
you shouldn't need to set this.

returns:: A code::DrawGridY::.


METHOD:: numTicks
Set the emphasis::approximate:: number of grid lines ("ticks") for each axis. If set, the number of ticks is fixed and code::numTicks:: takes precedence over link::#-tickSpacing::. If code::nil::, the number of grid lines change with the view size, constrained by the code::tickSpacing::.
Default: code::nil::.

See link::#Testing in a UserView#Examples:: below.

argument:: numx
emphasis::Approximate:: number of grid lines ("ticks") for the x-axis.

argument:: numy
emphasis::Approximate:: number of grid lines for the y-axis.

discussion::
The resulting number of ticks is approximate because of the underlying algorithm in link::Classes/GridLines#-niceNum::, which tries to find suitable values for the grid lines based on the data range and your requested code::numTicks::. You can observe the behavior of code::GridLines:-niceNum:: with this snippet:
code::
(
g = GridLines([0, 200].asSpec);
"requested / returned".postln;
(0, 3 .. 21).do({ |ntck|
  "% / %\n".postf(ntck, g.niceNum(ntck, true))
})
)
::


METHOD:: tickSpacing
Set the emphasis::minimum:: spacing between grid lines ("ticks") for each axis. The number of grid lines will change with the view size, but won't be spaced less than this code::tickSpacing::, allowing you to control the density of grid lines. However if link::#-numTicks:: is not code::nil::, it takes precedence over code::tickSpacing::.

See link::#Testing in a UserView#Examples:: below.

argument:: xpx
emphasis::Minimum:: spacing between grid lines ("ticks") on the x-axis (pixels, default: 64).

argument:: ypx
emphasis::Minimum:: spacing between grid lines on the y-axis (pixels, default: 64).


METHOD:: test
Test this code::DrawGrid:: object by creating a window with a view showing the code::DrawGrid:: in its current state.

code::
d = DrawGrid(nil, \freq.asSpec.grid, \amp.asSpec.grid);
d.linePattern_(FloatArray[10.0, 5.0, 2.0, 5.0]);
d.test;
::


See link::#*test:: for creating a code::DrawGrid:: and previewing it immediately. See also link::#Testing in a UserView#Examples:: below.


METHOD:: copy
Safely make a copy of this object and its working members.

returns:: A new code::DrawGrid::.


PRIVATE:: clearCache, init

EXAMPLES::

SUBSECTION:: Basic use in a UserView

code::
(
w = Window.new.front;
u = UserView(w,w.bounds.size.asRect);

// The spec defines its preferred grid system
x = \lofreq.asSpec.grid; // x grid lines
y = \amp.asSpec.grid;    // y grid lines
i = 40;                  // grid inset

d = DrawGrid(u.bounds.size.asRect.insetBy(i), x, y);

u.drawFunc = { d.draw };
u.resize_(5);
u.onResize = { |u|
	d.bounds = u.bounds.size.asRect.insetBy(i)
};
)
::

SUBSECTION:: Testing and modifying

For testing the look and feel of your code::GridLines::, you can create and immediately render your code::DrawGrid:: using the link::#*test:: class method, or call link::#-test:: on your already-instantiated code::DrawGrid:: object.

Create and immediately render your code::DrawGrid:: using the link::#*test:: class method:
code::
(
c = ControlSpec.new(20,200, \lin, units: "myUnits");
g = c.grid;
~test = DrawGrid.test(g,g)
)
::

Modifying its properties then refresh the view to see the changes:
code::
// Modify the number of grid lines on each axis separately
~test.x.tickSpacing_(18); // fixed *density* of x ticks
~test.y.numTicks_(8);     // fixed *number* of y ticks (approximate)
~test.refresh;            // refresh the test view

~test.y.numTicks_(nil); // numTicks = nil: auto, follows -tickSpacing
~test.refresh;
::

The returned code::DrawGridTest:: is a subclass of code::DrawGrid::, so you can call all the methods you would on code::DrawGrid::. Convenience methods like link::#-numTicks:: and link::#-tickSpacing:: adjust both axes together:
code::
// Fixed number of grid lines; -numTicks takes precedence over -tickSpacing
~test.numTicks_(10, 10).tickSpacing_(30, 75).refresh;

// Dynamic number of grid lines; -tickSpacing determines minimum line spacing when resizing
~test.numTicks_(nil, nil).tickSpacing_(30, 75).refresh;
~test.linePattern_(FloatArray[10.0, 5.0, 2.0, 5.0]).refresh;
::

Alternatively, you can instantiate your code::DrawGrid::, modify its properties, then preview it with the link::#-test:: method:
code::
(
// Instantiate a new DrawGrid, modify it
var xspec = ControlSpec.new(20, 2000, \lin, units: "xUnits");
var yspec = ControlSpec.new(20, 200, \lin, units: "yUnits");
d = DrawGrid.new(horzGrid: xspec.grid, vertGrid: yspec.grid);
d.x.tickSpacing_(25); // set its properties before testing
)
// Test it, returning and displaying a "test version" of it for more modification
~test = d.test;
::
